apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def properties = new Properties()
properties.load(rootProject.file('local.properties').newDataInputStream())

android {
  compileSdkVersion project.findProperty("android.compileSdkVersion") as int
  defaultConfig {
    applicationId "jp.bucketeer.sample"
    minSdkVersion project.findProperty("android.minSdkVersion") as int
    targetSdkVersion project.findProperty("android.targetSdkVersion") as int
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    def API_KEY = properties.getProperty("api_key") ?: System.getenv("API_KEY")
    def API_URL = properties.getProperty("api_url") ?: System.getenv("API_URL")
    buildConfigField("String", "API_KEY", "\"${API_KEY}\"")
    buildConfigField("String", "API_URL", "\"${API_URL}\"")
  }
  signingConfigs {
    release {
      storeFile rootProject.file("debug.keystore")
      storePassword "android"
      keyAlias "androiddebugkey"
      keyPassword "android"
    }
  }
  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt')
      signingConfig signingConfigs.release
    }
  }
}

dependencies {
  def USE_DEV_SDK = (properties.getProperty("sample.use_dev_sdk") ?: System.getenv("USE_DEV_SDK")) == "true"

  if (USE_DEV_SDK) {
    implementation(project(":bucketeer"))
  } else {
    implementation "jp.bucketeer:sdk-android:0.0.4"
  }
  implementation 'com.facebook.stetho:stetho:1.5.0'
  implementation 'androidx.appcompat:appcompat:1.0.0'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
  implementation 'com.google.android.material:material:1.0.0'
  implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

  androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
  androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0'
  androidTestImplementation 'androidx.test:rules:1.1.1'
  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.KOTLIN"
}
repositories {
  mavenCentral()
}
