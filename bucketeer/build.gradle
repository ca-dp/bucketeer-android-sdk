apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.protobuf'

def properties = new Properties()
properties.load(rootProject.file('local.properties').newDataInputStream())

android {
  namespace = "jp.bucketeer.sdk"

  compileSdkVersion project.findProperty("android.compileSdkVersion") as int
  defaultConfig {
    def isTest = !gradle.startParameter.taskNames.findAll { it.contains("Test") }.isEmpty()
    if (isTest) {
      minSdkVersion project.findProperty("android.testMinSdkVersion") as int
    } else {
      minSdkVersion project.findProperty("android.minSdkVersion") as int
    }
    targetSdkVersion project.findProperty("android.targetSdkVersion") as int

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    testInstrumentationRunnerArguments clearPackageData: 'true'
  }

  buildTypes {
    debug {
      def API_KEY = properties.getProperty("api_key") ?: System.getenv("API_KEY")
      def API_URL = properties.getProperty("api_url") ?: System.getenv("API_URL")
      buildConfigField("String", "API_KEY", "\"${API_KEY}\"")
      buildConfigField("String", "API_URL", "\"${API_URL}\"")
    }
    release {
      minifyEnabled true
      consumerProguardFiles 'proguard-rules.pro'
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  sourceSets {
    String sharedTestDir = 'src/sharedTest/java'
    test {
      java.srcDir sharedTestDir
    }
    androidTest {
      java.srcDir sharedTestDir
    }
  }
  compileOptions {
    sourceCompatibility 1.8
    targetCompatibility 1.8
  }
  packagingOptions {
    exclude 'google/protobuf/descriptor.proto'
  }
  lintOptions {
    disable 'InvalidPackage'
    xmlReport true
  }
}

dependencies {
  // gRPC
  implementation "io.grpc:grpc-okhttp:$versions.GRPC"
  implementation "io.grpc:grpc-protobuf-lite:$versions.GRPC"
  implementation "io.grpc:grpc-stub:$versions.GRPC"
  compileOnly "javax.annotation:javax.annotation-api:$versions.JAVAX_ANNOTATION"
  // This provides the protobuf.Duration for event.proto
  compileOnly "com.google.api.grpc:proto-google-common-protos:$versions.COMMON_PROTOS"
  implementation "com.squareup.okhttp:okhttp:2.7.5"
  implementation "com.google.protobuf:protobuf-javalite:$versions.PROTO_JAVALITE"

  // Kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.KOTLIN"

  // Google Play Services
  compileOnly "com.google.android.gms:play-services-basement:$versions.GOOGLE_PLAY_SERVICES"

  // Test
  testImplementation "junit:junit:$versions.JUNIT"
  testImplementation "org.robolectric:robolectric:$versions.ROBOLECTRIC"
  testImplementation "org.mockito:mockito-core:$versions.MOCKITO_ANDROID"
  testImplementation "org.mockito.kotlin:mockito-kotlin:$versions.MOCKITO_KOTLIN"
  testImplementation "org.amshove.kluent:kluent-android:$versions.KLUENT"
  androidTestImplementation "androidx.test.espresso:espresso-core:$versions.ANDROIDX_TEST_ESPRESSO"
  androidTestImplementation "androidx.test.espresso:espresso-contrib:$versions.ANDROIDX_TEST_ESPRESSO"
  androidTestImplementation "androidx.test:rules:$versions.ANDROIDX_TEST"
  androidTestImplementation "androidx.test.ext:junit:$versions.ANDROIDX_TEST_EXT"
  androidTestImplementation "org.mockito:mockito-android:$versions.MOCKITO_ANDROID"
  androidTestImplementation("org.mockito.kotlin:mockito-kotlin:$versions.MOCKITO_KOTLIN") {
    exclude group: 'org.mockito', module: 'mockito-core'
  }
  androidTestImplementation("org.amshove.kluent:kluent-android:$versions.KLUENT") {
    exclude group: 'org.mockito', module: 'mockito-core'
  }
  androidTestUtil "androidx.test:orchestrator:$versions.ANDROIDX_TEST"
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$versions.PROTOC"
  }
  plugins {
    javalite {
      artifact = "com.google.protobuf:protoc-gen-javalite:$versions.PROTOC_JAVALITE"
    }
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$versions.GRPC"
    }
  }
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {
          option 'lite'
        }
      }
      task.plugins {
        grpc {
          option 'lite'
        }
      }
    }
  }
}

apply from: "publish.gradle"
